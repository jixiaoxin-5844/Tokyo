plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

//App名字
String AppName = "punch"

// 获取local.properties的内容
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

// 渠道列表
String[] channelArray = properties.getProperty('channel.list').split(',')

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "com.hyt.punchapp"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // 包名
        applicationId properties.getProperty("${AppName}_applicationId")
        // 默认值
        manifestPlaceholders = [
                //BD_LOCATION_AK          : properties.getProperty("${AppName}_ak"),
                CHANNEL_VALUE : "default",
                BUGLY_APP_ID          : properties.getProperty("${AppName}_BUGLY_APP_ID")
        ]

    }

    // 多渠道打包
    flavorDimensions "default"
    productFlavors {
        for (int i = 0; i < channelArray.size(); i++) {
            def channel = channelArray[i]
            "${channel}" {
                dimension "default"
                manifestPlaceholders = [
                        APP_ICON           : "@mipmap/logo_0",
                        UMENG_CHANNEL_VALUE: channel
                ]
                //  }
            }
        }
    }

    signingConfigs {
        config {
            storeFile file(properties.getProperty("keystroe_storeFile"))
            storePassword properties.getProperty("keystroe_storePassword")
            keyAlias properties.getProperty("keystroe_keyAlias")
            keyPassword properties.getProperty("keystroe_keyPassword")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation project(path: ':base_lib')

    //**********************************
    def room_version = "2.2.5"

    implementation 'com.tencent.bugly:crashreport:3.3.3'
    kapt "androidx.room:room-compiler:$room_version"


    //**********************************

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}